buildscript {
	repositories {
		jcenter()
		maven {
			url  "https://dl.bintray.com/uulib/uulib"
		}
	}
	dependencies {
		classpath 'org.uulib.gradletag:gradletag-git:0.2.0-alpha.2'
	}
}

apply plugin: 'org.uulib.reckon'
apply plugin: org.uulib.gradletag.git.GitTagPlugin


reckon {
	vcs = git(rootProject.grgit).withTagsMatching ~/${project.name}-(.+)/

	def stage = projectProperty 'stage' orElse 'development'
	def scope = projectProperty "${project.name}.scope" or 'scope' orElse 'patch'

	normalVersion = basedOn(stage) {
		normally scopedUsing(scope)
		when 'development' then '0.0.0'
	}

	preReleaseVersion = basedOn(stage) {
		normally stageUsing(stage)
		when 'final' then none
		when 'development' then compound {
			preRelease = timestamp(buildTime)
			buildMetadata = commitId
		}
	}
}

gradletag {
	tags {
		versionTag {
			tag = "${project.name}-${project.version}"
		}
	}
}
